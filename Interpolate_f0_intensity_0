# Interpolate F0 and intensity continuum
# For the two disyllabic words in a minimal pair of lexical stress with nearly the same duration and structure, 
# this script interpolates the F0 and intensity contour linearly in n steps,
# in which 1 and n are the original contours of word 1 (trochaic) and word 2 (iambic) respectively.
# Note that the duration of the two words *must be* (nearly) the same.

form Interpolate F0 and intensity continuum
	comment Directory of sound file 1 (the trochaic word)
	comment The new soundfile is based on sound file 1 
	text soundfile1 07_adjusted_VOORnaam
	comment Directory of sound file 2 (the iambic word)
	text soundfile2 08_adjusted_voorNAAM
	comment Sound file extension
	text extension .wav
	comment number of steps
	integer num 7
endform

# Read files
sound1 = Read from file: soundfile1$ + extension$
sound2 = Read from file: soundfile2$ + extension$

# Extract pitch tiers
select sound1
s1_dur = Get total duration
int_1 = Get intensity (dB)
To Pitch (filtered ac): 0.0, 50, 800, 15, 1, 0.03, 0.09, 0.50, 0.055, 0.35, 0.14
plus sound1
To Manipulation
Extract pitch tier

select sound2
s2_dur = Get total duration
int_2 = Get intensity (dB)
To Pitch (filtered ac): 0.0, 50, 800, 15, 1, 0.03, 0.09, 0.50, 0.055, 0.35, 0.14
plus sound1
To Manipulation
Extract pitch tier

# Extract the IntensityTier
select sound1
To Intensity: 100, 0, 0
Down to IntensityTier
removeObject: (sound1 + 8)
# Therefore (sound1 + 9) is the IntensityTier of sound1
select sound2
To Intensity: 100, 0, 0
Down to IntensityTier
removeObject: (sound2 + 9)
# Therefore (sound2 + 10) is the IntensityTier of sound2

# Create 10 ms time bins for interpolation
timebinsize = 0.01
nbins = floor(s1_dur/timebinsize)

for currentbin from 1 to nbins
	currentbin_start = (currentbin-1)*timebinsize
	currentbin_end = currentbin*timebinsize
	currentbin_mid = (currentbin_start + currentbin_end)/2
	# Select PitchTier of sound1
	select (sound1 + 4)
	f0_bin_s1[currentbin] = Get value at time... currentbin_mid
	# Select PitchTier of sound2
	select (sound2 + 6)
	f0_bin_s2[currentbin] = Get value at time... currentbin_mid
	# Select IntensityTier of sound1
	select (sound1 + 9)
	int_bin_s1[currentbin] = Get value at time... currentbin_mid
	# Select IntensityTier of sound2
	select (sound2 + 10)
	int_bin_s2[currentbin] = Get value at time... currentbin_mid
endfor

# Interpolate pitch and intensity tiers
for currentstep from 1 to num
	# Select the PitchTier of sound1
	select (sound1 + 4)
	Copy... f0interpol_'currentstep'

	# Interpolate pitch
	# First remove all original F0 points
	Remove points between... 0 's1_dur'
	
	# Then add a point for each time bin
	for currentbin from 1 to nbins
		currentbin_start = (currentbin-1)*timebinsize
		currentbin_end = currentbin*timebinsize
		currentbin_mid = (currentbin_start + currentbin_end)/2
		currentbin_f0_diff = f0_bin_s1[currentbin] - f0_bin_s2[currentbin]
		currentbin_f0_ratio = currentbin_f0_diff /(num-1)
		currentbin_f0_interpol = f0_bin_s1[currentbin] - currentbin_f0_ratio * (currentstep-1)
				
		Add point... 'currentbin_mid' 'currentbin_f0_interpol'
	endfor
	
	# Replace the pitch contour from the orginal sound1
	# Select the Manipulation tier of the original sound1
	select (sound1 + 3)
	Copy... interpol_'currentstep'
	plusObject: "PitchTier f0interpol_'currentstep'"
	Replace pitch tier
	minusObject: "PitchTier f0interpol_'currentstep'"
	Get resynthesis (overlap-add)
	f0interpolated  = selected()

	# Create an intensity tier for the sound with interpolated f0
	To Intensity: 100, 0, 0
	Down to IntensityTier
	removeObject: (f0interpolated + 1)
	select (f0interpolated + 2)
	
	# Interpolate intensity
	# First make the intensity a flat line by multiplying its reverse
	Formula... self*-1
	plus f0interpolated
	Multiply... yes
	intensityflat = selected()
	# Now we get a nearly flat line of intensity for each soundfile with manipulated f0
	# We need to multiply this sound with the intensity line we need (to create a continuum) 
	
	# We now create the intensity line
	# Select IntensityTier of sound1
	select (sound1 + 9)
	Copy... intensityinterpol_'currentstep'
	Remove points between... 0 's1_dur'
	# Then add a point for each time bin
	for currentbin from 1 to nbins
		currentbin_start = (currentbin-1)*timebinsize
		currentbin_end = currentbin*timebinsize
		currentbin_mid = (currentbin_start + currentbin_end)/2
		currentbin_int_diff = int_bin_s1[currentbin] - int_bin_s2[currentbin]
		currentbin_int_ratio = currentbin_int_diff /(num-1)
		currentbin_int_interpol = int_bin_s1[currentbin] - currentbin_int_ratio * (currentstep-1)
				
		Add point... 'currentbin_mid' 'currentbin_int_interpol'
	endfor
	plus intensityflat
	Multiply... yes
	Scale intensity: (int_1 + int_2)/2
	Rename... f0intensity_interpol_'currentstep'			
endfor

